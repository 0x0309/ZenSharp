using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Github.Ulex.ZenSharp.Core
{
  public class LiveTemplateMatcher
  {
    private _tree : GenerateTree;

    public this(tree : GenerateTree) {
      _tree = tree;
    }

    [Record]
    public class MatchResult {
      public Success : bool;
      public Tail : string;
      public Rules : list[LeafRule];

      public this(mr : bool) {
        Success = mr;
      }

      public Expand(prefix : string) : string {
        def loop(tail, input, acc){
          match (tail){
            | x :: xs => {def matchResult = x.Match(input); loop(xs, matchResult.Crop(input), acc + matchResult.Expand); }
            | _ => acc
          }
        }
        loop(Rules, prefix, "");
      }
    }

    public Match (prefix : string, scopename : string) : MatchResult
    {
      def scope = _tree.FindScope(scopename);
      def scopeRules = _tree.GlobalRules + scope.Rules;

      // circle reference fix
      mutable tryMatchRuleVar;

      def tryMathcLeafRule(rule : LeafRule, acc : MatchResult) : MatchResult {
        match (rule){
          | x is LeafRule.NonTerminal => tryMatchRuleVar(_tree.FindRule(x.Value, scopeRules).Rules, acc);
          | lr => {
              def leafMatch = lr.Match(acc.Tail);
              MatchResult(leafMatch.Success, leafMatch.Crop(acc.Tail), rule :: acc.Rules)
            }
        }
      }

      def tryMatchConcatRule (rules : list[LeafRule], acc : MatchResult) : MatchResult {
        match (rules) {
          | x :: xs => {
            def r = tryMathcLeafRule(x, acc);
            if (!acc.Success)
              r
            else
              tryMatchConcatRule(xs, r);
          }
          | _ => acc;
        }
      }

      def tryMatchRule (rules : list[ConcatRule], acc : MatchResult) : MatchResult {
        match (rules) {
          | x :: xs =>
            {
              def r = tryMatchConcatRule(x.Rules, acc);
              if (r.Success)
                r
              else
                tryMatchRule(xs, acc);
            };
          | _ => MatchResult(false, acc.Tail, acc.Rules);
        }
      }
      tryMatchRuleVar = tryMatchRule;

      def result = tryMatchRule(_tree.FindStartRule(scope).Rules, MatchResult(true, prefix, []));
      MatchResult(result.Success, result.Tail, result.Rules.Reverse());
    }
  }
}
